
model {
  
  for (i in 1:n) {
    
    # likelihood
    for (j in 1:m) {
      
      # count model
      Y[i,j] ~ dnegbin(pi[i,j], phi)
      
      pi[i,j] <- (1-zero[i,j])*(phi/(phi + lambda[i,j])) + zero[i,j]*0.9999
      
      log(lambda[i,j]) <- alpha.count[i] + log(pop[i]) + Z[j,1:p]%*%xi[1:p] +
          X[i,max(1,j-l):j]%*%theta[i,max(1,l-j+2):(l+1)]
      
      # zero Model
      logit(psi[i]) <- alpha.zero[i]
      zero[i,j] ~ dbern(psi[i])
    
    }
    
  }
  
  # random effects
  for (i in 1:n) {
    
    alpha.count[i] ~ dnorm(mu.count, tau.count)
    alpha.zero[i] ~ dnorm(mu.zero, tau.zero)
    
    for (k in 1:(l+1)) {
    
      theta[i,k] ~ dnorm(eta[k], omega[k])
      
    }
  
  }
  
  # priors
  # random interecept
  mu.zero ~ dnorm(0, 1e-10)
  tau.zero ~ dscaled.gamma(sig[1], 2)
  mu.count ~ dnorm(0, 1e-10)
  tau.count ~ dscaled.gamma(sig[1], 2)
  
  # zero and count model
  xi[1:p] ~ dmnorm(b[1:p], S[1:p,1:p])
  phi ~ dunif(0,50)
  
  # distributed lags
  eta[1:(l+1)] ~ dmnorm(a[1:(l+1)], R[1:(l+1),1:(l+1)])
  
  for (k in 1:(l+1)) {
  
    omega[k] ~ dscaled.gamma(sig[k], 2)
    sigma[k] <- 1/omega[k]
  
  }
  
}